"""Split input data into Dengue serotypes, add serotypes to sequence name and generate
sequence specific metadata.
"""
configfile: "config/config.yaml"
from grapevne_helper import import_grapevne

grapevne = import_grapevne(workflow)
globals().update(vars(grapevne))

outdir = config["output_namespace"]


rule process_dengue_data:
    input:
        metadata=input("infoTbl.csv"),
        fasta=input("Unaligned.fasta"),
        script=script("split_dengue.R"),
    output:
        fasta=output("Unaligned_{serotype}.fasta"),
        csv=output("Unaligned_{serotype}_infoTbl.csv"),
        tsv=output("Unaligned_{serotype}_infoTbl.txt"),
    params:
        output_dir=output(),
    log:
        log("process_data_{serotype}.log"),
    message:
        (
            "Split into serotype, add serotypes to sequence name and generate "
            "sequence specific metadata ({wildcards.serotype})"
        )
    conda:
        env("dengue.yaml")
    shell:
        """
        mkdir -p {params.output_dir}
        Rscript {input.script} \
            --metadata {input.metadata} \
            --fasta {input.fasta} \
            --outfile {params.output_dir}/Unaligned_ \
            > {log} 2>&1
        """


rule _test:
    input:
        output("Unaligned_Dengue_1.fasta"),
        output("Unaligned_Dengue_2.fasta"),
        output("Unaligned_Dengue_3.fasta"),
        output("Unaligned_Dengue_4.fasta"),
