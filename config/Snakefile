# Serotypes
serotype = ['Dengue_1', 'Dengue_2', 'Dengue_3', 'Dengue_4']

rule all:
    input:
       expand("auspice/dengue_{serotype}.json", serotype=serotype)

# Rule for creating necessary directories
rule create_directories:
    output:
        directory("logs"),
        directory("data"),
        directory("results")
    shell:
        "mkdir -p logs data results"

# Step 1: Acquisition of Genomic Data and Metadata from GenBank
rule acquire_data:
    output:
        zip="data/genbank_data.zip",
        metadata="data/metadata.tsv",
        fasta="data/ncbi_dataset/data/genomic.fna"
    params:
        date = "01/01/2022",
    log:
        "logs/acquire_data.log"
    message:
        "Acquiring Genomic Data and Metadata from NCBI database"
    conda:
        "config/ncbi_datasets_env.yaml"
    shell:
        """
        datasets download virus genome taxon "Dengue Virus" --filename {output.zip} --released-after {params.date} &> {log}
        datasets summary virus genome taxon "Dengue Virus" --released-after {params.date} --as-json-lines | dataformat tsv virus-genome > {output.metadata} 2>> {log}
        
        # Unzip the downloaded data
        unzip -d data {output.zip}
        """

# Step 2: Clean metadata and FASTA
rule process_genbank_data:
    input:
        script="code/clean_metadata_and_fasta_general.R",
        metadata="data/metadata.tsv",
        fasta="data/ncbi_dataset/data/genomic.fna"
    output:
        fasta_files ="results/Unaligned.fasta",
        info_tables_txt ="results/infoTbl.txt",
        info_tables_csv ="results/infoTbl.csv"
    params:
        start_date = "2000-01-01",
        end_date = "2023-12-24",
        host = "Homo sapiens"

    log:
        "logs/process_genomic_data.log"
    message:
        "Processing and cleaning data downloaded from NCBI"
    shell:
        """
        rscript {input.script} --metadata {input.metadata} --fasta {input.fasta} --start-date {params.start_date} --end-date {params.end_date} --host "{params.host}" --outfile_fasta {output.fasta_files} --outfile_csv {output.info_tables_csv} --outfile_tsv {output.info_tables_txt} > {log} 2>&1
        """

# Step 3: Split into serotype, add serotypes to sequence name and generate sequence specific metadata
rule process_dengue_data:
    input:
        script="code/split_dengue.R",
        metadata="results/infoTbl.csv",
        fasta="results/Unaligned.fasta"
    output:
        fasta_files = "results/Unaligned_{serotype}.fasta",
        info_tables_txt = "results/{serotype}_infoTbl.txt",
        info_tables_csv = "results/{serotype}_infoTbl.csv"
    log:
        "logs/process_{serotype}_data.log"
    message:
        "Split into serotype, add serotypes to sequence name and generate sequence specific metadata"
    shell:
        """
        rscript {input.script} --metadata {input.metadata} --fasta {input.fasta} --outfile_fasta {output.fasta_files} --outfile_csv {output.info_tables_csv} --outfile_tsv {output.info_tables_txt} > {log} 2>&1
        """

# Step 4: Sequence alignment
rule sequence_alignment:
    input:
        fasta_files = "results/Unaligned_{serotype}.fasta"
    output:
        align_dir = "results/Aligned_{serotype}/nextalign.aligned.fasta"
    log:
        "logs/sequence_{serotype}_alignment.log"
    conda:
        "config/alignment.yaml"
    message:
        "Running sequence alignment for Dengue serotypes"
    shell:
        """
        bash bash/align_sequences.sh > {log} 2>&1
        """

# Step 5: Segregating E gene and Whole Genomes and performing quaility control
rule split_genome_and_QC:
    input:
        fasta_files = "results/Aligned_{serotype}/nextalign.aligned.fasta"
    output:
        E_gene_dir = "results/{serotype}_EG.fasta",
        WG_gene_dir = "results/{serotype}_WG.fasta"
    params:
        wg_threshold = 0.29,
        eg_threshold = 0.05
    log:
        "logs/split_eg_and_wg_{serotype}.log"
    message:
        "Segregating E gene and whole genomes from aligned Dengue virus sequences and performing quality control."
    shell:
        """
        Rscript Code/Seperate_EG_and_WG.R  --fasta {input.fasta_files} --WG_threshold {params.wg_threshold} --EG_threshold {params.eg_threshold} --outfile_wg {output.WG_gene_dir} --outfile_eg {output.E_gene_dir} > {log} 2>&1
        """

# Step 6a: Subsampler DENV1
rule subsample_denv1:
    input:
        fasta_files = "results/Dengue_1_EG.fasta",
        metadata_files = "results/Dengue_1_infoTbl.csv"
    output:
        subsample_fasta = "results/subsampled_Dengue_1.fasta",
        subsample_csv = "results/subsampled_Dengue_1_infoTbl.csv",
        subsample_txt = "results/subsampled_Dengue_1_infoTbl.tsv"
    params:
        country = "Colombia",
        number_sequences = 500,
        prop_rd = 0.8,
        prop_or = 0.2
    log:
        "logs/subsample_denv1.log"
    message:
        "Subsampling Dengue 1 virus E gene sequences based on specified criteria."
    shell:
        """
        rscript Code/subsampler.R --metadata '{input.metadata_files}' --fasta '{input.fasta_files}' --country {params.country} --number_sequences {params.number_sequences} --prop_rd {params.prop_rd} --prop_or {params.prop_or} > {log} 2>&1
        """

# Step 6b: Subsampler DENV2
rule subsample_denv2:
    input:
        fasta_files = "results/Dengue_2_EG.fasta",
        metadata_files = "results/Dengue_2_infoTbl.csv"
    output:
        subsample_fasta = "results/subsampled_Dengue_2.fasta",
        subsample_csv = "results/subsampled_Dengue_2_infoTbl.csv",
        subsample_txt = "results/subsampled_Dengue_2_infoTbl.tsv"
    params:
        country = "Colombia",
        number_sequences = 500,
        prop_rd = 0.8,
        prop_or = 0.2
    log:
        "logs/subsample_denv2.log"
    message:
        "Subsampling Dengue 2 virus E gene sequences based on specified criteria."
    shell:
        """
        rscript Code/subsampler.R --metadata '{input.metadata_files}' --fasta '{input.fasta_files}' --country {params.country} --number_sequences {params.number_sequences} --prop_rd {params.prop_rd} --prop_or {params.prop_or} > {log} 2>&1
        """

# Step 6c: Subsampler DENV3
rule subsample_denv3:
    input:
        fasta_files = "results/Dengue_3_EG.fasta",
        metadata_files = "results/Dengue_3_infoTbl.csv"
    output:
        subsample_fasta = "results/subsampled_Dengue_3.fasta",
        subsample_csv = "results/subsampled_Dengue_3_infoTbl.csv",
        subsample_txt = "results/subsampled_Dengue_3_infoTbl.tsv"
    params:
        country = "Colombia",
        number_sequences = 500,
        prop_rd = 0.8,
        prop_or = 0.2
    log:
        "logs/subsample_denv3.log"
    message:
        "Subsampling Dengue 3 virus E gene sequences based on specified criteria."
    shell:
        """
        rscript Code/subsampler.R --metadata '{input.metadata_files}' --fasta '{input.fasta_files}' --country {params.country} --number_sequences {params.number_sequences} --prop_rd {params.prop_rd} --prop_or {params.prop_or} > {log} 2>&1
        """

# Step 6D: Subsampler DENV4
rule subsample_denv4:
    input:
        fasta_files = "results/Dengue_4_EG.fasta",
        metadata_files = "results/Dengue_4_infoTbl.csv"
    output:
        subsample_fasta = "results/subsampled_Dengue_4.fasta",
        subsample_csv = "results/subsampled_Dengue_4_infoTbl.csv",
        subsample_txt = "results/subsampled_Dengue_4_infoTbl.tsv"
    params:
        country = "Colombia",
        number_sequences = 500,
        prop_rd = 0.8,
        prop_or = 0.2
    log:
        "logs/subsample_denv4.log"
    message:
        "Subsampling Dengue 4 virus E gene sequences based on specified criteria."
    shell:
        """
        rscript Code/subsampler.R --metadata '{input.metadata_files}' --fasta '{input.fasta_files}' --country {params.country} --number_sequences {params.number_sequences} --prop_rd {params.prop_rd} --prop_or {params.prop_or} > {log} 2>&1
        """

# Step 7: Treebuilding
rule iqtree:
    input:
        aln = "results/subsampled_{serotype}.fasta"
    output:
        tree = "results/subsampled_{serotype}.fasta.tree"
    conda:
        "config/iqtree.yaml"
    params:
        model = "GTR+F+I"
    log:
        "logs/iqtree_{serotype}.log"
    message:
        "Inferring maximum likelihood phylogenetic trees for {wildcards.serotype} using IQ-TREE."
    shell:
        """
        iqtree2 -s {input.aln} -m {params.model} -redo > {log} 2>&1
        """


# Step 8: Building Time-Calibrated Trees
rule treetime:
    input:
        aln = "results/subsampled_{serotype}.fasta",
        ml_tree = "results/subsampled_{serotype}.fasta.tree",
        metadata = "results/subsampled_{serotype}_infoTbl.csv"
    output:
        tree = "results/timetrees/timetree_{serotype}.tree",
        branch_lengths = "results/timetrees/{serotype}_branch_lengths.json"
    conda:
        "config/nextstrain_all.yaml"
    log:
        "logs/treetime_{serotype}.log"
    message:
        "Inferring time-calibrated trees for each Dengue virus serotype."
    shell:
        """
        augur refine \
            --tree {input.ml_tree} \
            --alignment {input.aln} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.branch_lengths} \
            --timetree \
            --coalescent opt \
            --date-confidence \
            --date-inference marginal \
            --clock-filter-iqd 3 \
            --root best > {log} 2>&1
        """
# Step 9: Infer "ancestral" mutations across the tree
rule mutations:
    input:
        aln = "results/subsampled_{serotype}.fasta",
        time_tree = "results/timetrees/timetree_{serotype}.tree"
    output:
        mutations = "results/subsampled_{serotype}_mutations.json"
    conda:
        "config/nextstrain_all.yaml"
    log:
        "logs/mutations_{serotype}.log"
    message:
        "Infer ancestral mutations across the tree"
    shell:
        """
        augur ancestral \
            --tree {input.time_tree} \
            --alignment {input.aln} \
            --inference joint > {log} 2>&1
        """

# Step 10: Translate sequences
rule translation:
    input:
        mutations = "results/subsampled_{serotype}_mutations.json",
        time_tree = "results/timetrees/timetree_{serotype}.tree",
        ref_genomes = "reference_genomes/reference_{serotype}.gb"
    output:
        amino = "results/aa_muts_{serotype}.json"
    conda:
        "config/nextstrain_all.yaml"
    log:
        "logs/translations_{serotype}.log"
    params:
        genes = "E"
    message:
        "Translate sequences"
    shell:
        """
        augur translate \
            --tree {input.time_tree} \
            --ancestral-sequences {input.mutations} \
            --reference-sequence {input.ref_genomes} \
            --genes {params.genes} \
            --output {output.amino} > {log} 2>&1
        """

# Step 11: Discrete trait reconstruction
rule mugration:
    input:
        time_tree = "results/timetrees/timetree_{serotype}.tree",
        metadata = "results/subsampled_{serotype}_infoTbl.csv"
    output:
        traits = "results/traits_{serotype}.json"
    conda:
        "config/nextstrain_all.yaml"
    log:
        "logs/mugration_{serotype}.log"
    message:
        "Use Discrete Trait Reconstruction for country and state"
    shell:
        """
        augur traits \
            --tree {input.time_tree} \
            --metadata {input.metadata} \
            --columns Country State \
            --confidence \
            --output {output.traits} > {log} 2>&1
        """

# Step 12: Export for visualisation in Auspice

rule export:
    """Exporting data files for auspice"""
    input:
        tree = "results/timetrees/timetree_{serotype}.tree",
        metadata = "results/subsampled_{serotype}_infoTbl.csv",
        branch_lengths = "results/timetrees/{serotype}_branch_lengths.json",
        traits = "results/traits_{serotype}.json",
        aa_muts = "results/aa_muts_{serotype}.json",
        nt_muts = "results/subsampled_{serotype}_mutations.json",
        auspice_config = "config/auspice_config.json",
    output:
        auspice_json = "auspice/dengue_{serotype}.json",
    conda:
        "config/nextstrain_all.yaml"
    log:
        "logs/export_{serotype}.log"
    message:
        "Export for visualisation in Auspice"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.nt_muts} {input.aa_muts} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_json} > {log} 2>&1
        """
